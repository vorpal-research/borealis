#
# Copyright (C) 2006  Seznam.cz, a.s.
#
# $Id: configure.in 5069 2008-04-30 10:09:38Z vladimir.kobzev $
#
# DESCRIPTION
# configure template Cfgparser
#
# AUTHORS
# Vaclav Blazek <blazek@firma.seznam.cz>
#
# HISTORY
# 2006-02-13  (vasek)
#             Created.
#


# initialize autoconf
AC_INIT(cfgparser, 1.1.2, michal.bukovsky@firma.seznam.cz)
# initialize automake(use AC_INIT's arguments)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AC_PREREQ(2.50)

# tell autoconf where to find sources

AC_CONFIG_SRCDIR([src/cfgparser.cc])

# check for C++ compiler
AC_PROG_CXX

# check for std C headers
AC_HEADER_STDC

# check for libtool
AC_PROG_LIBTOOL

# This version number needs to be changed in several different ways for each
# release. Please read the libtool documentation (info libtool 'Updating
# version info') before touching this. (this is *.so version number).
VERSION_INFO="-version-info 0:0:0"
AC_SUBST(VERSION_INFO)

# specify path to dbglog
AC_ARG_WITH(dbglog,
    [  --with-dbglog=dbglog    specify path to dbglog ], [
        DBGLOG_PATH="$withval"
    ], [
        DBGLOG_PATH="/usr"
])

### define test-for-header macro
# $1 = header file name (i.e. "mdbg.h")
# function fids out the header or terminates configure script
AC_DEFUN([TEST_HEADER],[
FOUND="no"
AC_CHECK_HEADER($1,FOUND="yes")
if test "$FOUND" = "no"; then
  # try search dirs
  for SRCHDIR in \
      `pwd` \
      `pwd`/include \
      /usr/local/include \
      $DBGLOG_PATH/include \
      ;
  do
    AC_MSG_CHECKING(for $1 ($SRCHDIR))
    AC_TRY_CPP([#include "$SRCHDIR/$1"],FOUND="yes")
    AC_MSG_RESULT($FOUND)
    if test "$FOUND" = "yes"; then
      # add path to env
      CPPFLAGS="$CPPFLAGS -I$SRCHDIR"
      break
    fi
  done
fi
# check for result
if test "$FOUND" = "no"; then
  AC_MSG_ERROR(*** Cannot find $1 header!!! ***)
fi
])
### end macro


### define test-for-lib macro
# $1 = library file name (i.e. "mdbg")
# $2 = test function to link (i.e. "MLOG_LOG")
# $3 = other libs needed to link against lib in $1
# function fids out the library or terminates configure script
AC_DEFUN([TEST_LIB],[
FOUND="no"
AC_CHECK_LIB($1,$2,FOUND="yes"; LIBS="-l$1 $LIBS",,$3)
if test "$FOUND" = "no"; then
  # try search dirs
  SAVED_LIBS=$LIBS
  for SRCHDIR in \
      `pwd` \
      /usr/lib \
      $DBGLOG_PATH/lib \
	  ;
  do
    AC_MSG_CHECKING(for $2 in -l$1 ($SRCHDIR))
    LIBS="-l$1 $SAVED_LIBS -L$SRCHDIR $3"
    AC_TRY_LINK_FUNC($2, FOUND="yes")
    AC_MSG_RESULT($FOUND)
    if test "$FOUND" = "yes"; then
      # add path to env
      LIBS="-l$1 $SAVED_LIBS -L$SRCHDIR"
      break
    fi
  done
fi
# check for result
if test "$FOUND" = "no"; then
  AC_MSG_ERROR(*** Cannot find $1 library!!! ***)
fi
])
### end macro

# dbglog
AC_LANG_C
TEST_HEADER(dbglog.h)
TEST_LIB(dbglog,__dbglog)

# generate Makefile
AC_OUTPUT(version Makefile src/Makefile include/Makefile)
